import groovy.json.JsonOutput

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "androidx.navigation.safeargs.kotlin"
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.github.ben-manes.versions"
}

defaultTasks 'dependencyUpdates'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.jhoangamarra.condorlabstest"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.jhoangamarra.condorlabstest.CondorTestApplicationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

}


def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    //change the release channel (current, release-candidate, nightly)
    dependencyUpdates.gradleReleaseChannel = "current"

    // Example 3: using the full syntax
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }

    outputFormatter = { result ->
        def updatable = result.outdated.dependencies
        String outputDir = "${project.buildDir.path.replace(project.projectDir.path + '/', '')}/dependencyUpdates"
        //String outputDir = "/home/runner/work/CL-Challenge/CL-Challenge/build/dependencyUpdates"
        println("Directory ::")
        println(outputDir)

        if (!updatable.isEmpty()) {

            def outdatedDependencies = []
            updatable.each { dependency ->
                def data = [
                        group    : dependency.group,
                        name     : dependency.name,
                        version  : dependency.version,
                        available: dependency.available.release ?: dependency.available.milestone
                ]
                outdatedDependencies.add(data)
            }

            def jsonStr = JsonOutput.toJson(outdated: outdatedDependencies)
            def json_beauty = JsonOutput.prettyPrint(jsonStr)
            println(json_beauty)

            File filename = new File(outputDir, 'proreport2' + '.' + 'json')
            project.file(outputDir).mkdirs()
            project.file(filename).write(json_beauty)


            println('file name : ' + filename)

        }
    }


}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.1'
    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.1'

    // Utils(Glide, CircleImageView, Lottie, Spinner)
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.airbnb.android:lottie:3.7.1'
    implementation 'com.github.chivorns:smartmaterialspinner:1.5.0'

    //With concat adapter
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // Room
    implementation 'androidx.room:room-ktx:2.3.0'
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.okhttp3:okhttp:4.8.0'

    //Navigation Component
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    //Dagger2
    // implementation 'com.google.dagger:dagger:2.35.1'
    //kapt 'com.google.dagger:dagger-compiler:2.35.1'

    //Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.38.1'
    kapt 'com.google.dagger:hilt-compiler:2.38.1'

    //Retrieve viewModels easy form with Hilt
    implementation "androidx.activity:activity-ktx:1.3.1"

    //Testing
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    //Mockk
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation 'junit:junit:4.13.2'
    //for testing retrofit.
    testImplementation("com.squareup.okhttp3:mockwebserver:4.7.2")

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.21"


    // Instrumented Unit Tests
    androidTestImplementation "junit:junit:4.13"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:2.12.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "org.mockito:mockito-core:2.21.0"
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.38.1'
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.2.0"
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.38.1'
    debugImplementation "androidx.fragment:fragment-testing:1.3.0"


}